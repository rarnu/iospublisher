(*
Parameters:
  1: ProjectName
  2: SchemaName
  3: EnableBitcode(0 = false, 1 = true)
  4: AppStore Account
  5: AppStore Password
*)

program iospublisher;

{$mode objfpc}{$H+}

uses
  cthreads, Classes, sysutils, process;

const
  BUFSIZE = 1024;
  ALTOOLPATH = '/Applications/Xcode.app/Contents/Applications/Application Loader.app/Contents/Frameworks/ITunesSoftwareService.framework/Versions/A/Support/altool';

procedure innerRunCmd(cmd: string; params: TStringList);
var
  p: TProcess;
  byteRead: LongInt;
  buf: array[0..BUFSIZE - 1] of Byte;
  outstr: string = '';
begin
  p := TProcess.Create(nil);
  p.Executable:= cmd;
  p.Parameters.Assign(params);
  p.Options:= [poUsePipes, poStderrToOutPut];
  p.Execute;
  while (True) do begin
    byteRead:= p.Output.Read(buf, BUFSIZE);
    if (byteRead = 0) then Break;
    SetLength(outstr, byteRead);
    Move(buf[0], outstr[1], byteRead);
    Write(outstr);
  end;
  p.Free;
end;

function getWorkspace(AProjectName: string): string;
var
  src: TSearchRec;
  path: string;
  ret: string = '';
begin
  path := GetCurrentDir;
  if (not path.EndsWith('/')) then path += '/';
  if (FindFirst(path + '*.*', faAnyFile, src) = 0) then begin
    repeat
      if (src.Name = '.') or (src.Name = '..') then Continue;
      if (string(src.Name).EndsWith('.xcworkspace')) then begin
        ret := path + src.Name;
        Break;
      end;
    until FindNext(src) <> 0;
    FindClose(src);
  end;
  if (ret = '') then begin
    ret := AProjectName + '.xcodeproj';
  end;
  Exit(ret);
end;

function isUnderProjectPath(): Boolean;
var
  path: string;
  src: TSearchRec;
  ret: Boolean = False;
begin
  path := GetCurrentDir;
  if (not path.EndsWith('/')) then path += '/';
  if (FindFirst(path + '*.*', faAnyFile, src) = 0) then begin
    repeat
      if (src.Name = '.') or (src.Name = '..') then Continue;
      if (string(src.Name).EndsWith('.xcworkspace')) or (string(src.Name).EndsWith('.xcodeproj')) then begin
        ret := True;
        Break;
      end;
    until FindNext(src) <> 0;
    FindClose(src);
  end;
  Exit(ret);
end;

procedure generatePlist(ABitcode: Boolean);
var
  path: string;
begin
  path := GetCurrentDir;
  if (not path.EndsWith('/')) then path += '/';
  path += 'export.plist';
  with TStringList.Create do begin
    Clear;
    Add('<?xml version="1.0" encoding="UTF-8"?>');
    Add('<!DOCTYPE plist PUBLIC "-//Apple//DTD PLIST 1.0//EN" "http://www.apple.com/DTDs/PropertyList-1.0.dtd">');
    Add('<plist version="1.0">');
    Add('    <dict>');
    Add('        <key>method</key>');
    Add('        <string>app-store</string>');
    Add('        <key>uploadBitcode</key>');
    if (ABitcode) then Add('        <true/>') else Add('        <false/>');
    Add('        <key>uploadSymbols</key>');
    Add('        <true/>');
    Add('    </dict>');
    Add('</plist>');
    SaveToFile(path);
    Free;
  end;
end;

procedure clean(AQuiet: Boolean = True);
var
  params: TStringList;
begin
  params := TStringList.Create;
  params.Clear;
  params.Add('clean');
  params.Add('-configuration');
  params.Add('Release');
  if (AQuiet) then params.Add('-quiet');
  innerRunCmd('xcodebuild', params);
  params.Free;
end;

procedure compileProject(buildPath:string; schemaName: string; projectName: string; AQuiet: Boolean = True);
var
  params: TStringList;
  ws: string;
begin
  params := TStringList.Create;
  params.Clear;
  params.Add('archive');
  ws := getWorkspace(projectName);
  if (ws.EndsWith('.xcodeproj')) then begin
    params.Add('-project');
    params.Add(ws);
  end else begin
    params.Add('-workspace');
    params.Add(ws);
  end;
  params.Add('-scheme');
  params.Add(schemaName);
  params.Add('-configuration');
  params.Add('Release');
  params.Add('-archivePath');
  params.Add(buildPath + '/' + projectName + '.xcarchive');
  if (AQuiet) then params.Add('-quiet');
  innerRunCmd('xcodebuild', params);
  params.Free;
end;

procedure buildIpa(buildPath: string; projectPath: string; projectName: string; ipaPath: string; plistPath: string; AQuiet: Boolean = True);
var
  params: TStringList;
begin
  params := TStringList.Create;
  params.Clear;
  params.Add('-exportArchive');
  params.Add('-archivePath');
  params.Add(buildPath + '/' + projectName + '.xcarchive');
  params.Add('-configuration');
  params.Add('Release');
  params.Add('-exportPath');
  params.Add(ipaPath);
  params.Add('-exportOptionsPlist');
  params.Add(plistPath);
  if (AQuiet) then params.Add('-quiet');
  innerRunCmd('xcodebuild', params);
  params.Free;
end;

procedure validateApp(ipaPath: string; schemaName: string; userName: string; userPass: string);
var
  params: TStringList;
begin
  params := TStringList.Create;
  params.Clear;
  params.Add('--validate-app');
  params.Add('-f');
  params.Add(ipaPath + '/' + schemaName + '.ipa');
  params.Add('-u');
  params.Add(userName);
  params.Add('-p');
  params.Add(userPass);
  params.Add('-t');
  params.Add('ios');
  params.Add('--output-format');
  params.Add('xml');
  innerRunCmd(ALTOOLPATH, params);
  params.Free;
end;

procedure uploadApp(ipaPath: string; schemaName: string; userName: string; userPass: string);
var
  params: TStringList;
begin
  params := TStringList.Create;
  params.Clear;
  params.Add('--upload-app');
  params.Add('-f');
  params.Add(ipaPath + '/' + schemaName + '.ipa');
  params.Add('-u');
  params.Add(userName);
  params.Add('-p');
  params.Add(userPass);
  params.Add('-t');
  params.Add('ios');
  params.Add('--output-format');
  params.Add('xml');
  innerRunCmd(ALTOOLPATH, params);
  params.Free;
end;

procedure writeHelp();
begin
  WriteLn('Usage: iospublisher');
  WriteLn('    iospublisher <project name> <schema name> <enable bitcode> <appstore account> <appstore password>');
  WriteLn('    -- project name: your iOS project name');
  WriteLn('    -- schema name: schema name in the project');
  WriteLn('    -- enable bitcode or not: must be 0 or 1, 0 for disabled and 1 for enabled');
  WriteLn('    -- account for login into AppStore');
  WriteLn('    -- password for login into AppStore');
  WriteLn('');
end;

var
  currDir: string;
  ipaPath: string;
  // env
  projectPath: string;
  projectName: string;
  schemaName: string;
  buildPath: string;
  plistPath: string;
  userName: string;
  userPass: string;
  verbose: Boolean;
begin
  if (not isUnderProjectPath()) then begin
    WriteLn('iOS Pusblisher must be run under an iOS project.');
    Exit;
  end;

  if (ParamCount <> 5) and (ParamCount <> 6) then begin
    writeHelp();
    Exit;
  end;

  currDir:= GetCurrentDir;
  if (not currDir.EndsWith('/')) then currDir += '/';
  ipaPath:= currDir + 'IPADIR';
  if (not DirectoryExists(ipaPath)) then ForceDirectories(ipaPath);

  projectPath:= GetCurrentDir;
  projectName:= ParamStr(1);
  schemaName:= ParamStr(2);
  buildPath:= projectPath + '/build';
  plistPath:= projectPath + '/export.plist';

  generatePlist(ParamStr(3) = '1');
  userName:= ParamStr(4);
  userPass:= ParamStr(5);
  verbose := not (ParamStr(6) = '-v');

  clean(verbose);
  compileProject(buildPath, schemaName, projectName, verbose);
  buildIpa(buildPath, projectPath, projectName, ipaPath, plistPath, verbose);
  validateApp(ipaPath, schemaName, userName, userPass);
  uploadApp(ipaPath, schemaName, userName, userPass);
end.

